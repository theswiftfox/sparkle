//--------------------------------------------------------------------------------------
// File: cull.comp
//
// This file contains the GLSL Compute Shader to perform occlusion culling
// 
// Copyright (c) 2019 Patrick Gantner. All rights reserved.
//--------------------------------------------------------------------------------------
#version 450

struct BoundingSphere {
	vec3 center;
	float radius;
};

struct MeshData {
	mat4 model;
	BoundingSphere bb;
	uint firstIndex;
	uint indexCount;
	vec2 pad;
};

layout(binding = 0, std140) readonly buffer Objects {
    MeshData Meshes[ ];
};

struct DrawCommandIndexIndirect {
	uint indexCount;
	uint instanceCount;
	uint firstIndex;
	uint vertexOffset;
	uint firstInstance;
};

layout(binding=1, std430) writeonly buffer IndirectDraws {
    DrawCommandIndexIndirect indirectDraws[ ];
};

layout(binding=2) uniform UBO {
	vec4 frustumCube[6];
	vec3 cameraPosition;
	uint meshCount;
} ubo;

layout(binding=3) buffer OutBuffer {
    uint drawCount;
} outBuffer;

layout(local_size_x = 16) in;

bool insideFrustum(uint idx) {
	BoundingSphere bb = Meshes[idx].bb;
	mat4 model = Meshes[idx].model;
	vec4 pos = model * vec4(bb.center, 1.0);
	float scale = length(vec3(model[0][0], model[1][0], model[2][0]));
	float rad = scale * bb.radius;
	if (scale < 1.0) rad += 10.0; // workaround for very small bounding spheres not working...

	for (uint i = 0; i < 6; ++i) {			
		if (dot(pos, ubo.frustumCube[i]) + rad < 0.0) {
			return false;
		}
	}
	return true;
}

layout(local_size_x = 16) in;

void main() {
	uint idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;
	if (idx >= ubo.meshCount) return;

	if (idx == 0) {
		atomicExchange(outBuffer.drawCount, 0);
	}

	if (insideFrustum(idx)) {
		indirectDraws[idx].instanceCount = 1;
		indirectDraws[idx].firstIndex = Meshes[idx].firstIndex;
		indirectDraws[idx].indexCount = Meshes[idx].indexCount;
		indirectDraws[idx].vertexOffset = 0;
		indirectDraws[idx].firstInstance = 0;
		atomicAdd(outBuffer.drawCount, 1);
	}
	else {
		indirectDraws[idx].instanceCount = 0;
	}
}